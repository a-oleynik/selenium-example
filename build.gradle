buildscript {
    ext {
        allureVersion = "2.32.0"
        allureReportVersion = "2.35.1"
        aspectjVersion = "1.9.24"
        log4jVersion = "2.24.3"
        jacksonVersion = "2.20.1"
        seleniumVersion = "4.39.0"
        slf4jVersion = "2.0.17"
        testNGVersion = "7.11.0"
        lombokVersion = "1.18.42"
    }
}

plugins {
    id 'java-library'
    id('io.qameta.allure') version '3.0.1'
    id 'idea'
    id 'org.gradle.test-retry' version '1.6.4'
    id 'com.github.ben-manes.versions' version '0.53.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

description = 'Gradle selenium example'
group = 'com.oleynik.selenium.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations.configureEach {
    resolutionStrategy {
        force("org.aspectj:aspectjweaver:$aspectjVersion")
    }
}

dependencies {
    implementation("org.testng:testng:$testNGVersion")
    implementation("io.qameta.allure:allure-testng:$allureVersion")
    implementation("io.qameta.allure:allure-java-commons:$allureVersion")
    implementation("org.aspectj:aspectjweaver:$aspectjVersion")
    implementation("org.seleniumhq.selenium:selenium-java:$seleniumVersion")
    implementation("org.aeonbits.owner:owner:1.0.12")
    implementation("org.apache.poi:poi-ooxml:5.5.1")
    implementation("com.opencsv:opencsv:5.12.0")
    implementation("org.apache.logging.log4j:log4j-to-slf4j:$log4jVersion")
    implementation("org.slf4j:slf4j-simple:$slf4jVersion")
    implementation('org.assertj:assertj-core:3.27.6')
    implementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    testCompileOnly("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

allure {
    version = allureReportVersion

    useTestNG {
        version = allureVersion
    }
}

tasks.register('cleanAllureReport', Delete) {
    delete layout.buildDirectory.file("reports/allure-report/allureReport")
}

tasks.register('excelReport', JavaExec) {
    description = "Collects and generate consolidated Excel report"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "com.oleynik.gradle.selenium.example.framework.utils.ReportUtils"
}

test {
    systemProperties System.getProperties()
    systemProperty "file.encoding", "utf-8"
    useTestNG {
        parallel = 'classes'
        threadCount = 3
        useDefaultListeners = true
        outputDirectory = layout.buildDirectory.file("reports/testng").get().asFile
    }
    dependsOn cleanTest
    dependsOn cleanAllureReport
    testLogging.exceptionFormat = 'full'
    testLogging.showStandardStreams = true

    String includedGroupsProperty = System.getProperty("groups")
    boolean isIncludedGroupsProperty = includedGroupsProperty != null
    String excludedGroupsProperty = System.getProperty("exclude")
    boolean isExcludedGroupsProperty = excludedGroupsProperty != null
    if (isIncludedGroupsProperty && isExcludedGroupsProperty) {
        useTestNG {
            includeGroups includedGroupsProperty
            excludeGroups excludedGroupsProperty
        }
    } else if (isIncludedGroupsProperty && !isExcludedGroupsProperty) {
        useTestNG {
            includeGroups includedGroupsProperty
        }
    } else if (!isIncludedGroupsProperty && isExcludedGroupsProperty) {
        useTestNG {
            excludeGroups excludedGroupsProperty
        }
    } else {
        useTestNG {
        }
    }

    retry {
        failOnPassedAfterRetry = true
        maxFailures = 100
        maxRetries = 1
    }
}

test.finalizedBy allureReport, excelReport

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:unchecked"
    }
}
