buildscript {
    ext {
        allureVersion = "2.29.1"
        allureReportVersion = "2.32.2"
        aspectjVersion = "1.9.22.1"
        log4jVersion = "2.24.3"
        jacksonVersion = "2.18.2"
        seleniumVersion = "4.29.0"
        slf4jVersion = "2.0.16"
        junit5Version = "5.12.0"
        junitPlatformVersion = "1.12.0"
        lombokVersion = "1.18.36"
    }
}

plugins {
    id 'java-library'
    id('io.qameta.allure') version '2.12.0'
    id 'idea'
    id 'org.gradle.test-retry' version '1.6.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

description = "Gradle selenium example"
group 'com.oleynik.selenium.example'
version '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations.all {
    resolutionStrategy {
        force("org.slf4j:slf4j-api:$slf4jVersion",
                "org.apache.logging.log4j:log4j-api:$log4jVersion"
        )
    }
}

dependencies {
    implementation("org.junit.jupiter:junit-jupiter-api:$junit5Version")
    implementation("org.junit.jupiter:junit-jupiter-engine:$junit5Version")
    implementation("org.junit.jupiter:junit-jupiter-params:$junit5Version")
    implementation("org.junit.platform:junit-platform-runner:$junitPlatformVersion")
    implementation("io.qameta.allure:allure-junit5:$allureVersion")
    implementation("io.qameta.allure:allure-java-commons:$allureVersion")
    implementation("org.aspectj:aspectjweaver:$aspectjVersion")
    implementation("org.seleniumhq.selenium:selenium-java:$seleniumVersion")
    implementation("org.aeonbits.owner:owner:1.0.12")
    implementation("org.apache.poi:poi-ooxml:5.3.0")
    implementation("com.opencsv:opencsv:5.10")
    implementation("org.apache.logging.log4j:log4j-to-slf4j:$log4jVersion")
    implementation("org.slf4j:slf4j-simple:$slf4jVersion")
    implementation('org.assertj:assertj-core:3.27.3')
    implementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    testCompileOnly("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

allure {
    version = allureReportVersion

    useJUnit5 {
        version = allureVersion
    }
}

task cleanAllureReport(type: Delete) {
    delete "build\\reports\\allure-report\\allureReport"
}

task excelReport(type: JavaExec) {
    description = "Collects and generate consolidated Excel report"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "com.oleynik.gradle.selenium.example.framework.utils.ReportUtils"
}

test {
    systemProperty "file.encoding", "utf-8"
    useJUnitPlatform {
    }
    testLogging.exceptionFormat = 'full'
    testLogging.showStandardStreams = true

    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.config.strategy", "dynamic")
    systemProperty("junit.jupiter.extensions.autodetection.enabled", "true")
    dependsOn cleanTest
    dependsOn cleanAllureReport
    testLogging.showStandardStreams = true
    maxParallelForks = 3

    retry {
        failOnPassedAfterRetry = true
        maxFailures = 100
        maxRetries = 1
    }
}

test.finalizedBy allureReport, excelReport

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:unchecked"
    }
}
