buildscript {
    ext {
        allureVersion = "2.32.0"
        allureReportVersion = "2.35.1"
        aspectjVersion = "1.9.24"
        log4jVersion = "2.24.3"
        jacksonVersion = "2.21.0"
        seleniumVersion = "4.40.0"
        slf4jVersion = "2.0.17"
        junit5Version = "6.1.0-M1"
        junitPlatformVersion = "6.1.0-M1"
        lombokVersion = "1.18.42"
    }
}

plugins {
    id 'java-library'
    id('io.qameta.allure') version '3.0.1'
    id 'idea'
    id 'org.gradle.test-retry' version '1.6.4'
    id 'com.github.ben-manes.versions' version '0.53.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

description = 'Gradle selenium example'
group = 'com.oleynik.selenium.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations.configureEach {
    resolutionStrategy {
        force("org.aspectj:aspectjweaver:$aspectjVersion")
    }
}

dependencies {
    implementation("org.junit.jupiter:junit-jupiter-api:$junit5Version")
    implementation("org.junit.jupiter:junit-jupiter-engine:$junit5Version")
    implementation("org.junit.jupiter:junit-jupiter-params:$junit5Version")
    implementation("org.junit.platform:junit-platform-reporting:$junitPlatformVersion")
    implementation("io.qameta.allure:allure-junit5:$allureVersion")
    implementation("io.qameta.allure:allure-java-commons:$allureVersion")
    implementation("org.aspectj:aspectjweaver:$aspectjVersion")
    implementation("org.seleniumhq.selenium:selenium-java:$seleniumVersion")
    implementation("org.aeonbits.owner:owner:1.0.12")
    implementation("org.apache.poi:poi-ooxml:5.5.1")
    implementation("com.opencsv:opencsv:5.12.0")
    implementation("org.apache.logging.log4j:log4j-to-slf4j:$log4jVersion")
    implementation("org.slf4j:slf4j-simple:$slf4jVersion")
    implementation('org.assertj:assertj-core:3.27.7')
    implementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    testCompileOnly("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

allure {
    version = allureReportVersion

    useJUnit5 {
        version = allureVersion
    }
}

tasks.register('cleanAllureReport', Delete) {
    delete layout.buildDirectory.file("reports/allure-report/allureReport")
}

tasks.register('excelReport', JavaExec) {
    description = "Collects and generate consolidated Excel report"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "com.oleynik.gradle.selenium.example.framework.utils.ReportUtils"
}

test {
    systemProperties System.getProperties()
    systemProperty "file.encoding", "utf-8"
    useJUnitPlatform()
    testLogging.exceptionFormat = 'full'
    testLogging.showStandardStreams = true

    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.config.strategy", "dynamic")
    systemProperty("junit.jupiter.extensions.autodetection.enabled", "true")
    dependsOn cleanTest
    dependsOn cleanAllureReport
    maxParallelForks = 3

    String includedTagsProperty = System.getProperty("includeTags")
    boolean isIncludedTagsProperty = includedTagsProperty != null
    String excludedTagsProperty = System.getProperty("excludeTags")
    boolean isExcludedTagsProperty = excludedTagsProperty != null
    if (isIncludedTagsProperty && isExcludedTagsProperty) {
        useJUnitPlatform {
            includeTags includedTagsProperty
            excludeTags excludedTagsProperty
        }
    } else if (isIncludedTagsProperty && !isExcludedTagsProperty) {
        useJUnitPlatform {
            includeTags includedTagsProperty
        }
    } else if (!isIncludedTagsProperty && isExcludedTagsProperty) {
        useJUnitPlatform {
            excludeTags excludedTagsProperty
        }
    } else {
        useJUnitPlatform {
        }
    }

    retry {
        failOnPassedAfterRetry = true
        maxFailures = 100
        maxRetries = 1
    }

    finalizedBy allureReport, excelReport
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:unchecked"
    }
}
