buildscript {
    ext {
        allureVersion = "2.30.0"
        allureReportVersion = "2.32.2"
        aspectjVersion = "1.9.24"
        log4jVersion = "2.24.3"
        jacksonVersion = "2.20.0"
        seleniumVersion = "4.35.0"
        slf4jVersion = "2.0.17"
        testNGVersion = "7.11.0"
        lombokVersion = "1.18.40"
    }
}

plugins {
    id 'java-library'
    id('io.qameta.allure') version '3.0.0'
    id 'idea'
    id 'org.gradle.test-retry' version '1.6.2'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

description = 'Gradle selenium example'
group = 'com.oleynik.selenium.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations.configureEach {
    resolutionStrategy {
        force("org.aspectj:aspectjweaver:$aspectjVersion")
    }
}

dependencies {
    implementation("org.testng:testng:$testNGVersion")
    implementation("io.qameta.allure:allure-testng:$allureVersion")
    implementation("io.qameta.allure:allure-java-commons:$allureVersion")
    implementation("org.aspectj:aspectjweaver:$aspectjVersion")
    implementation("org.seleniumhq.selenium:selenium-java:$seleniumVersion")
    implementation("org.aeonbits.owner:owner:1.0.12")
    implementation("org.apache.poi:poi-ooxml:5.4.1")
    implementation("com.opencsv:opencsv:5.12.0")
    implementation("org.apache.logging.log4j:log4j-to-slf4j:$log4jVersion")
    implementation("org.slf4j:slf4j-simple:$slf4jVersion")
    implementation('org.assertj:assertj-core:3.27.3')
    implementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    testCompileOnly("org.projectlombok:lombok:$lombokVersion")
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

allure {
    version = allureReportVersion

    useTestNG {
        version = allureVersion
    }
}

tasks.register('cleanAllureReport', Delete) {
    delete layout.buildDirectory.file("reports/allure-report/allureReport")
}

tasks.register('excelReport', JavaExec) {
    description = "Collects and generate consolidated Excel report"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "com.oleynik.gradle.selenium.example.framework.utils.ReportUtils"
}

test {
    systemProperty "file.encoding", "utf-8"
    useTestNG {
        parallel = 'classes'
        threadCount = 3
        useDefaultListeners = true
        outputDirectory = layout.buildDirectory.file("reports/testng").get().asFile
    }
    dependsOn cleanTest
    dependsOn cleanAllureReport
    testLogging.exceptionFormat = 'full'
    testLogging.showStandardStreams = true

    retry {
        failOnPassedAfterRetry = true
        maxFailures = 100
        maxRetries = 1
    }
}

test.finalizedBy allureReport, excelReport

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:unchecked"
    }
}
